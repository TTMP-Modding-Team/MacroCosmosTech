buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://jitpack.io' }
        mavenCentral()
    }
    dependencies {
        classpath("com.github.GregTechCE:ForgeGradle:FG_2.3-SNAPSHOT"){
            changing = true
        }
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'

// Fetch version from source because having to write version in two separate place is dumb
version = getVersionFromSource()
String getVersionFromSource(){
    File file = file("src/main/java/ttmp/macrocosmos/MacroCosmosMod.java")
    String version = file.find { String s->
        return s.contains("String VERSION")
    }
    return version.substring(version.indexOf('"')+1, version.lastIndexOf('"'))
}

group = "ttmp.macrocosmos"
archivesBaseName = "MacroCosmosTech-1.12.2"

compileJava.options.encoding = 'UTF-8'

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
    makeObfSourceJar = false
}

repositories {
    maven {
        name = "ChickenBones maven"
        url = "https://chickenbones.net/maven/"
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
}

dependencies {
    deobfCompile "codechicken-lib-1-8:CodeChickenLib-1.12.2:3.2.3.358:universal"

    // compile against the JEI API but do not include it at runtime
    // compileOnly "mezz.jei:jei_1.12.2:4.15.0.268:api"
    // at runtime, use the full JEI jar
    compile "mezz.jei:jei_1.12.2:4.15.0.268"

    compile "crafttweaker:CraftTweaker2:1.12:4.1.20.674"

    // deobfCompile "pixelmon:Pixelmon-1.12.2:8.3.6:universal"  // Not possible because fuck ForgeGradle
    compileOnly files("libs/Pixelmon-1.12.2-8.3.6-universal-deobf.jar")
    deobfCompile "gregtech-ce-unofficial:gregtech-1.12.2:2.1.1:beta"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}